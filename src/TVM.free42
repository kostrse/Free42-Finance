@ Origianl sources decompiled from TVM program shared by Dave Britten on SwissMicros calculator forum
@ https://forum.swissmicros.com/viewtopic.php?p=9057#p9057
@
@ Changes
@ - Remove global lables for menu programs

@ TVM main menu
LBL 00
CLMENU
"N"
KEY 1 XEQ 21			@ Key "N"
"I%YR"
KEY 2 XEQ 22			@ Key "I%YR"
"PV"
KEY 3 XEQ 23			@ Key "PV"
"PMT"
KEY 4 XEQ 24			@ Key "PMT"
"FV"
KEY 5 XEQ 25			@ Key "FV"
"P/YR"
KEY 6 XEQ 26			@ Key "P/YR"
KEY 7 GTO 01			@ Second menu
KEY 8 GTO 01			@ Second menu
MENU
STOP
GTO 00					@ Menu loop

@ TVM second menu
LBL 01
CLMENU
"×12N"
KEY 1 XEQ 31			@ Key "×12N"
"BEG"
FS? 01
├"•"
KEY 2 XEQ 32			@ Key "BEG"
"END"
FC? 01
├"•"
KEY 3 XEQ 33			@ Key "END"
"PRINT"
KEY 4 XEQ 34			@ Not implemented
"CLR"
KEY 5 XEQ 35			@ Clear finance variables
"AMRT"
KEY 6 GTO 36			@ Key "AMRT"
KEY 7 GTO 00			@ Main menu
KEY 8 GTO 00			@ Main menu
MENU
STOP
GTO 01					@ Menu loop

@ Key "AMRT"
LBL 36
XEQ 50					@ Clear amortization
@ Amortization menu
LBL 02
CLMENU
"#P"
KEY 1 XEQ 41			@ Key "#P"
"INT"
KEY 2 XEQ 42			@ Display interest payment
"PRIN"
KEY 3 XEQ 43			@ Display principal payment
"BAL"
KEY 4 XEQ 44			@ Display remaining balance
"NEXT"
KEY 5 XEQ 45
"TABLE"
KEY 6 GTO 04			@ Table menu
KEY 7 GTO 03			@ Amortization second menu
KEY 8 GTO 03			@ Amortization second menu
KEY 9 GTO 00			@ TVM main menu
MENU
STOP
GTO 02					@ Menu loop

@ Amortization second menu
LBL 03
CLMENU
"CLR"
KEY 1 XEQ 50			@ Clear amortization
KEY 7 GTO 02			@ Amortization menu
KEY 8 GTO 02			@ Amortization menu
MENU
STOP
GTO 03					@ Menu loop

@ Table menu
LBL 04
CLMENU
"FIRST"
KEY 1 XEQ 46			@ Not implemented
"LAST"
KEY 2 XEQ 47			@ Not implemented
"INCR"
KEY 3 XEQ 48			@ Not implemented
"GO"
KEY 6 XEQ 49			@ Not implemented
KEY 9 GTO 02			@ Amortization menu
MENU
STOP
GTO 03					@ Menu loop

@ Clear finance variables
LBL 35
XEQ 90					@ Save stack
CLX
STO "PMT'"
STO "BAL"
STO "PRIN"
STO "INT"
STO "#P"
STO "FV"
STO "PMT"
STO "PV"
STO "I%YR"
STO "N"
12
STO "P/YR"
XEQ 91					@ Restore whole stack

@ Key "END"
LBL 33
CF 01
RTN

@ Key "BEG"
LBL 32
SF 01
RTN

@ Clear amortization
LBL 50
XEQ 90					@ Save stack
RCL "PMT"
RND
STO "PMT'"
RCL "PV"
RND
STO "BAL"
0
STO "#P"
STO "ΣP"
STO "INT"
STO "PRIN"
STO "FIRST"
STO "LAST"
1
STO "INCR"
XEQ 91					@ Restore whole stack
RTN

@ Key "N"
LBL 21
FC?C 22
XEQ 51					@ Calculate number of periods
STO "N"
VIEW "N"
RTN

@ Key "I%YR"
LBL 22
FC?C 22
XEQ 52					@ Calculate interest rate
STO "I%YR"
VIEW "I%YR"
RTN

@ Key "PV"
LBL 23
FC?C 22
XEQ 53					@ Calculate present value
STO "PV"
VIEW "PV"
RTN

@ Key "PMT"
LBL 24
FC?C 22
XEQ 54					@ Calculate periodic payment
STO "PMT"
VIEW "PMT"
RTN

@ Key "FV"
LBL 25
FC?C 22
XEQ 55					@ Calculate future value
STO "FV"
VIEW "FV"
RTN

@ Key "P/YR"
LBL 26
STO "P/YR"
VIEW "P/YR"
RTN

@ Key "×12N"
LBL 31
XEQ 90					@ Save stack
12
*
STO "N"
VIEW "N"
CF 22
XEQ 93					@ Restore Y,Z,T, leave result in X
GTO 00

LBL "SPPV"
XEQ 90					@ Save stack
XEQ 15
+/-
*
E^X
GTO 94					@ Restore Z->Y,T->Z,T, leave result in X

LBL "SPFV"
XEQ 90					@ Save stack
XEQ 15
*
E^X
GTO 94					@ Restore Z->Y,T->Z,T, leave result in X

LBL 15
X<>Y
100
/
LN1+X
X<>Y
RTN

LBL "USPV"
XEQ 90					@ Save stack
RCL ST Y
X<>Y
XEQ 15
+/-
*
E^X-1
+/-
X<>Y
100
/
/
GTO 94					@ Restore Z->Y,T->Z,T, leave result in X

LBL "USFV"
XEQ 90					@ Save stack
RCL ST Y
X<>Y
XEQ 15
*
E^X-1
X<>Y
100
/
/
GTO 94					@ Restore Z->Y,T->Z,T, leave result in X

@ Calculate number of periods
LBL 51
XEQ 90					@ Save stack
XEQ 11
RCL* "PMT"
RCL+ "PV"
1/X
RCL "PV"
RCL+ "FV"
+/-
*
LN1+X
XEQ 10					@ Calculate interest rate for 1 period
LN1+X
/
STO "N"
GTO 92					@ Restore X->Y,Y->Z,Z->T, lift stack with new result

@ Calculate interest rate
LBL 52
XEQ 90					@ Save stack
PGMSLV "TVM"
RCL "I%YR"
SOLVE "I%YR"
GTO 92					@ Restore X->Y,Y->Z,Z->T, lift stack with new result

@ TVM equation for solver (for calculating I%/YR)
LBL 70
XEQ 11
XEQ 13
*
RCL* "PMT"
XEQ 14
RCL* "PV"
+
RCL+ "FV"
RTN

@ Calculate present value
LBL 53
XEQ 90					@ Save stack
XEQ 13
RCL* "PMT"
XEQ 11
*
RCL+ "FV"
XEQ 14
/
+/-
STO "PV"
GTO 92					@ Restore X->Y,Y->Z,Z->T, lift stack with new result

@ Calculate periodic payment
LBL 54
XEQ 90					@ Save stack
XEQ 14
RCL* "PV"
RCL+ "FV"
XEQ 13
XEQ 11
*
/
+/-
STO "PMT"
GTO 92					@ Restore X->Y,Y->Z,Z->T, lift stack with new result

@ Calculate future value
LBL 55
XEQ 90					@ Save stack
XEQ 13
RCL* "PMT"
XEQ 11
*
XEQ 14
RCL* "PV"
+
+/-
STO "FV"
GTO 92					@ Restore X->Y,Y->Z,Z->T, lift stack with new result

@ Calculate interest rate for 1 period
LBL 10
RCL "I%YR"
RCL/ "P/YR"
100
/
RTN

LBL 11
XEQ 10					@ Calculate interest rate for 1 period
1/X
XEQ 12					@ Return 0/1 for payment at beginning/end of period
+
RTN

@ Return 0/1 for payment at beginning/end of period
LBL 12
0
FS? 01
10^X
RTN

LBL 13
XEQ 10					@ Calculate interest rate for 1 period
LN1+X
RCL* "N"
2
/
ENTER
E^X
2
*
X<>Y
SINH
*
RTN

LBL 14
XEQ 10					@ Calculate interest rate for 1 period
LN1+X
RCL* "N"
E^X
RTN

@ Amortization Key "#P"
LBL 41
STO "#P"
LBL 45
XEQ 60					@ Amortize #P payments
FS? 91					@ If amortization error
RTN
XEQ 90					@ Save stack
"#P="
RCL "#P"
AIP
├" PMTS: "
RCL "FIRST"
AIP
├"-"
RCL "LAST"
AIP
XEQ 91					@ Restore whole stack
AVIEW
RTN

@ Display interest payment
LBL 42
RCL "INT"
VIEW "INT"
RTN

@ Display principal payment
LBL 43
RCL "PRIN"
VIEW "PRIN"
RTN

@ Display remaining balance
LBL 44
RCL "BAL"
VIEW "BAL"
RTN

@ Amortize #P payments
LBL 60
CF 91					@ Unset amortization error
XEQ 90					@ Save stack
0
STO "INT"
STO "PRIN"
RCL "#P"
CPX?
GTO 67
MAT?
GTO 67
X≤0?
GTO 67
ENTER
IP
X≠Y?
GTO 67
STO "_P"
STO "INCR"
SF 90
RCL "ΣP"
X≠0?
CF 90
FC? 01
CF 90
1
+
STO "FIRST"
RCL "#P"
RCL+ "ΣP"
STO "ΣP"
STO "LAST"
LBL 62
RCL "BAL"
RCL* "I%YR"
RCL/ "P/YR"
100
/
RND
FS?C 90
CLX
ENTER
ABS
RCL "PMT"
SIGN
*
STO+ "INT"
R↓
RCL+ "PMT'"
STO+ "PRIN"
STO+ "BAL"
DSE "_P"
GTO 62
RCL "#P"
RCL "BAL"
RCL "PRIN"
RCL "INT"
XEQ 95					@ Restore LastX only, leave results in X,Y,Z,T
CLV "_P"
RTN

LBL 67
SF 91					@ Set amortization error
"Invalid #P"
AVIEW
GTO 91					@ Restore whole stack

@ Move TVM variables to front
LBL 80
XEQ 90					@ Save stack
"INCR"
1
XEQ 81
"LAST"
0
XEQ 81
"FIRST"
0
XEQ 81
"PMT'"
0
XEQ 81
"BAL"
0
XEQ 81
"PRIN"
0
XEQ 81
"INT"
0
XEQ 81
"ΣP"
0
XEQ 81
"#P"
0
XEQ 81
"P/YR"
12
XEQ 81
"FV"
0
XEQ 81
"PMT"
0
XEQ 81
"PV"
0
XEQ 81
"I%YR"
0
XEQ 81
"N"
0
XEQ 81
XEQ 91					@ Restore whole stack
RTN

@ Move 1 variable to front, create with default if necessary
LBL 81
ASTO ST Y
SF 25
SF 30
RCL IND ST Y
FS?C 25
CLV IND ST Y
STO IND ST Y
RTN

@ Save stack
LBL 90
STO "_XFIN"
R↓
STO "_YFIN"
R↓
STO "_ZFIN"
R↓
STO "_TFIN"
R↓
X<> ST L
STO "_LFIN"
X<> ST L
RTN

@ Restore whole stack
LBL 91
RCL "_LFIN"
STO ST L
RCL "_TFIN"
RCL "_ZFIN"
RCL "_YFIN"
RCL "_XFIN"
GTO 99					@ Clear saved stack

@ Restore X->Y,Y->Z,Z->T, lift stack with new result
LBL 92
RCL "_LFIN"
STO ST L
R↓
RCL "_ZFIN"
RCL "_YFIN"
RCL "_XFIN"
R^
GTO 99					@ Clear saved stack

@ Restore Y,Z,T, leave result in X
LBL 93
RCL "_XFIN"
STO ST L
R↓
RCL "_TFIN"
RCL "_ZFIN"
RCL "_YFIN"
R^
GTO 99					@ Clear saved stack

@ Restore Z->Y,T->Z,T, leave result in X
LBL 94
RCL "_XFIN"
STO "_LFIN"
R↓
RCL "_TFIN"
RCL "_TFIN"
RCL "_ZFIN"
R^
GTO 99					@ Clear saved stack

@ Restore LastX only, leave results in X,Y,Z,T
LBL 95
X<> ST L
CLX
RCL "_LFIN"
X<> ST L
GTO 99					@ Clear saved stack

@ Clear saved stack
LBL 99
CLV "_XFIN"
CLV "_YFIN"
CLV "_ZFIN"
CLV "_TFIN"
CLV "_LFIN"
RTN

@ TVM entry-point function
LBL "TVM"
FS? 45
GTO 70					@ TVM equation for solver (for calculating I%/YR)
XEQ 80					@ Move TVM variables to front
GTO 00					@ Display TVM menu
END
